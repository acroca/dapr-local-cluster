load('ext://helm_resource', 'helm_resource', 'helm_repo')
load('./zipkin/Tiltfile', 'zipkin')
load('./redis/Tiltfile', 'redis', 'redis_pubsub_component', 'redis_state_component', 'redis_workflowstate_component')
load('./postgres/Tiltfile', 'postgres', 'postgres_state_component', 'postgres_workflowstate_component')
load('./kafka/Tiltfile', 'kafka', 'kafka_pubsub_component')
load('./pulsar/Tiltfile', 'pulsar', 'pulsar_pubsub_component')

k8s_kind('Component')

def dapr_dev():
  local_resource('dapr-images',
                dir='../dapr',
                cmd='mise exec dapr@1.16 -- dapr uninstall -k && make build docker-push',
                env={
                  'HA_MODE': 'true',
                  'DAPR_REGISTRY': 'localhost:5001/dapr',
                  'DAPR_TAG': 'dev',
                  'DAPR_TEST_NAMESPACE': 'default',
                  'DAPR_NAMESPACE': 'default',
                  'TARGET_OS': 'linux',
                  'TARGET_ARCH': 'arm64',
                  'GOOS': 'linux',
                  'GOARCH': 'arm64',
                  'LOG_LEVEL': 'debug'
                },
                labels=['core'])

  helm_resource('dapr', '../dapr/charts/dapr',
              release_name='dapr',
              flags=[
                  '--set', 'global.ha.enabled=true',
                  '--set', 'global.tag=dev-linux-arm64',
                  '--set', 'global.registry=localhost:5001/dapr',
                  '--set', 'global.logAsJson=true',
                  '--set', 'global.daprControlPlaneOs=linux',
                  '--set', 'global.daprControlPlaneArch=arm64',
                  '--set', 'dapr_placement.logLevel=debug',
                  '--set', 'dapr_sentry.logLevel=debug',
                  '--set', 'dapr_sidecar_injector.sidecarImagePullPolicy=Always',
                  '--set', 'global.imagePullPolicy=Always',
                  '--set', 'global.imagePullSecrets=',
                  '--set', 'global.mtls.enabled=true',
                  '--set', 'dapr_placement.cluster.forceInMemoryLog=true',
                  '--set', 'dapr_scheduler.replicaCount=1',
                  '--set', 'dapr_scheduler.cluster.storageSize=100Mi',
                  '--set', 'dapr_scheduler.cluster.inMemoryStorage=false',
                  '--set', 'dapr_scheduler.logLevel=debug',
                ],
              resource_deps=['dapr-images'],
              labels=['core']
              )

def dapr_release(version='1.16.2'):
  helm_repo('dapr-helm-repo', 'https://dapr.github.io/helm-charts', labels=['core'])
  helm_resource('dapr', 'dapr/dapr',
    release_name='dapr',
    flags=[
      '--set', 'global.ha.enabled=true',
      '--set', 'global.mtls.enabled=true',
      '--version', version,
    ],
    resource_deps=['dapr-helm-repo'],
    labels=['core'])



def dapr(version='dev', pubsub_backend='redis', pubsub_variant='', state_backend='redis'):
  local('helm repo update')
  zipkin()
  if version == 'dev':
    dapr_dev()
  else:
    dapr_release(version)

  if pubsub_backend == 'redis' or state_backend == 'redis':
    redis()
  if state_backend == 'postgres':
    postgres()
  if pubsub_backend == 'kafka':
    kafka(pubsub_variant)
  if pubsub_backend == 'pulsar':
    pulsar()

  if pubsub_backend == 'redis':
    redis_pubsub_component(pubsub_variant)
  elif pubsub_backend == 'kafka':
    kafka_pubsub_component(pubsub_variant)
  elif pubsub_backend == 'pulsar':
    pulsar_pubsub_component(pubsub_variant)
  else:
    fail('Unsupported pubsub backend: %s' % pubsub_backend)

  if state_backend == 'redis':
    redis_state_component()
    redis_workflowstate_component()
  elif state_backend == 'postgres':
    postgres_state_component()
    postgres_workflowstate_component()
  else:
    fail('Unsupported state backend: %s' % state_backend)
